// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x , b=false , sel=zx , out=x2 );
    Not16(in=x2 , out=x3 );
    Mux16(a=x2 , b=x3 , sel=nx , out=x4 );

    Mux16(a=y , b=false , sel=zy , out=y2 );
    Not16(in=y2 , out=y3 );
    Mux16(a=y2 , b=y3 , sel=ny , out=y4 );

    Add16(a = x4, b = y4 , out = sum );
    And16(a= x4, b= y4 , out= aAndb );
    Mux16(a= aAndb, b= sum, sel=f , out= of );

    Not16(in=of , out=nof );
    Mux16(a=of , b=nof , sel=no , out=outf );
    And16(a=outf , b=true , out=out );

    And16(a=true , b=outf , out[0..7]=p1);
    And16(a=true , b=outf , out[8..15]=p2);
    Or8Way(in=p1 , out=or1 );
    Or8Way(in=p2 , out=or2 );
    Or(a=or1 , b=or2 , out=or);
    Not(in=or , out=zr );


    And16(a=outf , b=true , out[15]=ng );

}